# 8 June 2019

## Matt Parsons "What does Free buy us?"

Actions in a billing system:

```haskell
data BillingProgram
  = GetUserBalance
  | GetUserLastPaymentDate
  | CancelSubscription
  | ChargeUser
  | SendLateNotice
```

How those actions are executed:

```haskell
data BillingState
  = BillingState
  { userId :: UserId
  , userBalance :: Double
  , userSubscription :: SubscriptionId
  , lastPaymentDate :: Day
  }

interpret :: BillingProgram -> StateT BillingState IO ()
interpret GetUserBalance = do
  id <- gets userId
  balance <- liftIO $ Stripe.getUserBalanceId id
  modify (\s -> s { userBalance = balance })
interpret GetUserLastPaymentDate = do
  ...
```

So to do everything, just create a `[BillingProgram]` and use `mapM_` to
interpret all the actions.

Problems:

- all state is stored in the interpreter
- the subscription is stored in the interpreter

Using the state monad this way is a bit like an OOP approach.
All logic depends on a stateful context.

Next step is to embed the data dependencies in `BillingState`.

```haskell
data BillingProgram
  = GetUserBalance UserId
  | GetUserLastPaymentDate UserId
  | CancelSubscription UserId PlanId
  | ChargeUser UserId Double
  | SendLateNotice PlanId Email
```

Now there is no need for state.

```haskell
interpret :: BillingProgram -> IO ()
interpet (GetUserBalance userId) = do
  Stripe.getUserBalance userId
interpret (GetUserLastPaymentDate userId) = do
  Stripe.getLastPaymentDate userId
interpret (CancelSubscription userId planId) = do
  ...
```

Problem:

`GetUserBalance` and `GetUserLastPaymentDate` are queries.
They have meaningful return values and we'd want to do something with the return
value.
But the return type of `interpret` is `IO ()`.

Two approaches:

- Refactor the data type to not have queries
- Refactor the data type to include the actions we want to take with the queries

### Without queries

This leads to a huge data type with all of the special cases for what we'd want
to do with a user balance or a user last payment date.

```haskell
data BillingProgram
  = CancelBillingSubscriptionIfTooOld UserId SubscriptionId
  | IfBalanceIsTooLowSendNotice UserId
  ...
```

This gets a little crazy.

### With queries

The problem with the current setup is that commands can't depend on previous
commands.
If you have a `GetUserBalance` command, then some other command can't depend on
the value of `GetUserBalance`.
You can't sequence these commands.
This can be fixed by adding a `BillingProgram` parameter to each of the data
constructors.
To sequence the commands, put the next command in the sequence as the parameter.
To pass data from one command to the next, make that parameter a function.
`Done` is used to indicate that there is no next step in the sequence.
And the `ret` parameter is used to specify a return type, so you can evaluate
a billing program to a value.

```haskell
data BillingProgram ret
  = GetUserBalance UserId (Double -> BillingProgram ret)
  | GetUserLastPaymentDate UserId (Day -> BillingProgram ret)
  | CancelSubscription UserId (BillingProgram ret)
  | ChargeUser UserId Double (BillingProgram ret)
  | SendLateNotice PlanId Email (BillingProgram ret)
  | Done ret
```

### Extending programs

Now the problem is composability.
How can we combine programs?
How can we sequence programs a second program can depend on the return value of
the first program?

```haskell
andThen :: BillingProgram a -> (a -> BillingProgram b) -> BillingProgram b
andThen (Done ret) mkProgram = mkProgram ret
```

So `mkProgram` constructs a second `BillingProgram` using the return value of
the first `BillingProgram`.
This requires us to traverse the first `BillingProgram`.

```haskell
andThen :: BillingProgram a -> (a -> BillingProgram b) -> BillingProgram b
andThen (Done ret) mkProgram = mkProgram ret
andThen (GetUserBalance userId next) mkProgram =
  GetUserBalance userId (\balance -> andThen (next balance) mkProgram)
andThen (GetUserLastPaymentDate userId next) mkProgram =
  ...
```

Here are a couple of functions that create billing programs:

```haskell
chargeOrEmail :: User -> Subscription -> BillingProgram Bool
chargeOrEmail user sub =
  GetUserBalance (userId user) $ \userBalance ->
    if userBalance >= subPrice sub
    then ChargeUser (userId user) (subPrice sub) (Done True)
    else SendLateNotice (userId user) (userEmail user) (Done False)

billingProgram :: User -> [Subscription] -> BillingProgram ()
billingProgram _ [] =
  Done ()
billingProgram user (sub:subs) =
  GetUserBalance uid $ \balance ->
    if balance > price then
      ChargeUser uid price theRest
    else
      SendLateNotice plan (userEmail user)
        $ GetUserLastPaymentDate uid
        $ \day -> if day < 60daysago
            then CancelSubscription uid plan theRest
            else theRest
  where
    uid = userId user
    price = subPrice sub
    plan = planPrice sub
    theRest = billingProgram user subs
```

By writing some helper functions that construct `BillingProgram` and writing a
Monad instance for `BillingProgram`, you get this:

```haskell
billingProgram :: User -> [Subscription] -> BillingProgram ()
billingProgram user subs = forM_ subs $ \sub -> do
  let uid = userId user
      price = subPrice sub
      plan = subPlan sub
  balance <- getUserBalance uid
  if balance > price
    then do
      chargeUser uid price
    else do
      day <- getUserLastPaymentDate uid
      when (day < 60daysago) $ do
        cancelSubscription uid plan
```

So these are all functions that just construct `BillingProgram`. 
We still have to interpet a billing program.

```haskell
interpet :: BillingProgram a -> IO a
interpret Done a = pure a
interpret (ChargeUser uid price next) = do
  Stripe.chargeUser uid price
  interpret next
interpret (SendLateNotice plan email next) = do
  Email.sendUserLateNotice plan email
  interpret next
interpret ...
```

First factor out the `Done` constructor.

```haskell
data Free f a
  = Done a
  | Free (f (Free f a))
```

```haskell
foldFree :: (Functor f, Monad m) => (forall a. f a -> m a) -> Free f a -> m a
foldFree morph (Done a) = return a
foldFree morph (Free fa) = do
  a <- morph fa
  foldFree morph a
```

`foldFree` enables you to do something at each step, without handling the 
recursion explicitly.

```haskell
data TerminalF next
  = GetLine (String -> next)
  | PrintLine String next

type Terminal = Free TerminalF

interpret :: Terminal a -> IO a
interpret = foldFree morph
  where 
    morph :: TermF a -> IO a
    morph (GetLine next) = 
      next <$> getLine
    morph (PrintLine s n) = do
      putStrLn s
      pure n
```

