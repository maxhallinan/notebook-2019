# 06 Aug 2019

Tags: LOGO

**Todo**

- Define a specification for Logo
  - What primitive commands/operators does the language include?
  - What is the language syntax?
  - What are the semantics?
- Work on the Lisp stepper

**Questions**

- Do I want to closely follow the old Logo or do I want to make a language based
  on Logo?

## _Logo Manual_ by Hal Abelson, Nat Goodman, Lee Rudolph

> Secondly, we believe that the syntactic details of a computer language are of
> insignificant importance compared to considerations of how the language is 
> used. 
> 
> Page 2

In the following examples, a line starting with `?` signifies a terminal prompt
in the LOGO environment.

### Basics

**primitives**: built-in "words" that the computer understands.

There are two types of primitives:

**commands**: imperatives that tell the computer to do something.
**operations**: take inputs and produce outputs.

All complete LOGO statements are imperative.
You cannot evaluate an operation except as part of a command statement.

```
SUM 1 2
```

produces this error:

```
YOU DON'T SAY WHAT TO DO WITH 3
```

but this is valid:

```
PRINT SUM 1 2
```

`TO`: primitive used to define a procedure. Used to "teach the computer a new 
word".

`PRINT`: takes one input and prints it to the screen followed by a newline

`TYPE`: prints input to screen without newline

`SUM`: outputs the sum of two numbers

Defining a procedure:

- `<procedure name>` cannot be the name of a Logo primitive or an existing 
  procedure.
- The procedure name is followed by parameter names, prefixed with dots (`:`).

```
TO <procedure name> :<input 1> :<input 2>
<line number><line>
END
```

### Interactive procedure editing

The manual describes a workflow that is different from working in a text file.
The user is working at a terminal, in an interactive Logo environment like a 
REPL.
You write as well as execute a Logo program within the environment. 
But you can tell the environment to write your program to a file.

For this purpose, there are a few editing commands: `TO`, `EDIT` (`ED`), and 
`END`.
You use `TO FOO :BAR :BAZ` to start defining a new procedure `FOO` with 
parameters `:BAR` and `:BAZ`.
You use `EDIT FOO` to jump back into the `FOO` context and modify its 
definition.
You use `END` to signal that this is the end of the definition and to move
back up to the global namespace.

There are a few more commands having to do with editing text.
This is interesting - it seems at least a little that the syntax is really 
determined or influenced by this particular style of writing/editing programs.
It doesn't seem necessarily true that this syntax makes sense outside of that
context.

> While you are defining/editing a procedure you can still do most of the things
> you usually can do in the LOGO world: use a display, a turtle, a music box, or
> other device; evaluate procedures &mdash; even the one you are in the midst of
> defining/editing. _However you cannot define/edit any other procedure._ That 
> is, `TO` and `EDIT` are invalid commands once you are in editing mode.
> 
> Page 6

### Comments

```
! text between exclamation points is a comment !
! text between an exclamation point and a line end is a comment
```

### Words

A word is a string of characters unbroken by a space or newline character.
A word is indicated by prefixing it with a quote.
The word is all of the characters between the quote and the following space or
return character.

```
"WORD
"TWO "WORDS
```

A word cannot contain:

- space and newline characters
- left and right square brackets: `[` and `]`

A word can contain quotation marks:

```
?PRINT "A"
A"
?PRINT ""
"
```

A word can contain no characters, this is called "the empty word":

```
?PRINT "

```

A percentage sign in a word is printed as a space:

```
?PRINT "X%X
X X
```

```
TO BOX
10 PRINT "XXXX
20 PRINT "X%%X
30 PRINT "X%%X
40 PRINT "XXXX
END
?BOX
XXXX
X  X
X  X
XXXX
```

### List of Words

Words can be ordered in lists.

Syntax:

- words are not quoted
- enclosed by square brackets
- separated by one or more spaces (extra spaces are ignored)
- a newline character within a list is equivalent to a space

```
?PRINT [A WORD LIST]
A WORD LIST

?PRINT [A BUNCH    OF  SPACES]
A BUNCH OF SPACES

?PRINT [A
       NEWLINE
       CHARACTER
       ]
A NEWLINE CHARACTER
```

Just like there is an empty word, there is also an empty list: `[]`.

### Manipulating words and lists

Operations:

Taking words and lists apart:

- `COUNT`: Takes one input. If the input is a word, it outputs the number of 
  letters in the word. If the input is a list, it outputs the number of words in
  the list.
- `FIRST`: Takes one input. If the input is a word, it outputs the first 
  character in the word. If the output is a list, it outputs the first word in
  the list.
- `LAST`: Similar to `FIRST` except returning the last item instead of the 
  first.
- `BUTFIRST`: Outputs all but the first letter of a word; all but the first word
  of a list.
- `BUTLAST`: Similar to `BUTFIRST` except excluding the last item.

Constructing words and lists:

`WORD`: takes two words and puts them together to construct a new word

`SENTENCE`: takes two inputs:
  - combines two lists two make a new list
  - combines a word and a list to make a new list
  - combines two words to make a new list

`REQUEST`: waits for the user to type in a list and then outputs that list

```
TO AGREE
10 PRINT [TYPE SOMETHING YOU LIKE]
20 PRINT (SENTENCE [I LIKE] REQUEST [TOO])
END
?AGREE
TYPE SOMETHING YOU LIKE
>PICKLE JELLO WITH PEANUT BUTTER
I LIKE PICKLE JELLO WITH PEANUT BUTTER TOO
```

When `SENTENCE` is enclosed with parentheses, it is made to take more than 2
inputs.
This also works for `WORD`, `PRINT`, and `TYPE`.

`REQUEST` always outputs a list.
`TYPEIN` always outputs a word.
`TYPEIN` is like the `FIRST` of a `REQUEST`.

`FPRINT` (Full PRINT) is another print command which is useful for debugging.
This will print values exactly as they are, for example it will print the 
brackets enclosing a list. `PRINT` excludes the brackets.

### Numbers and Arithmetic

**Infix forms**

- `+`
- `-`
- `*`
- `/` for the quotient of integer division, 3/4 outputs 0
- `\\` for the remainder of integer division, 3\4 outputs 3
- `-` for unary minus (effectively substracts the operand from 0)

**Precedence**

- `*`, `/`, `\\` have the higher precedence than `+` and `-`.
- All infix operators have higher precedence than prefix operators.

**Numerical conditionals**

- `<`: less than, only valid for numerical operands
- `>`: greater than, only valid for numerical operands
- `=`: equal to, operands can be any kind of LOGO object, e.g. words

```
?PRINT 1 = 1
TRUE
?PRINT 1 < 2
TRUE
?PRINT 2 > 1
TRUE
```

This operators have lower precedence than arithmetic operators.
This is so arithmetic can be done before the comparison.

**Prefix forms**

The arithmetic operations have prefix forms:

- `SUM`
- `DIFFERENCE`
- `PRODUCT`
- `QUOTIENT`
- `REMAINDER`
- `GREATER`
- `LESS`
- `EQUAL`

```
?PRINT (SUM 1 2)
3
?PRINT (SUM 1 2 3 4 5 6 7)
28
```

**Random**

`RANDOM`: takes no arguments and outputs a single random number.

**Floating point numbers**

Also has floating point numbers in two formats: exponential format and decimal
format.
Exponential format is used when the number is too large or small to be 
represented with seven digits and a decimal point.

**Numbers as words**

Numbers are LOGO words.
All word operations work on numbers.

```
?PRINT FIRST 123
1
?PRINT BUTFIRST 123
23
```
**NUMBERP**

`NUMBERP`: Takes one input. If that input is a number in the acceptable range 
for its respective type, `NUMBERP` outputs `TRUE`. If the input is a number 
outside of that range or not a number at all, outputs `FALSE`.

### Arrays


Can have one, two, or three dimensions.
There are three types of arrays: integer, floating point, and pointer.
Arrays used 0-based indexing.

`DEFINEARRAY`: creates an array.
Takes up to five arguments: name of array, number of dimensions, and the array
type (0 for integer, 1 for floating point, 2 for pointer).
If the array has more than one dimension, the call must be enclosed in 
parentheses.

An array is initialized with all of the elements set to zero.

`STORE`: assign a value to a specific element of an array.

```
STORE <array name> <coordinates of desired element> <value to be assigned to the element>
```

`GET`: reference the value of a specific element.

```
GET <array name> <coordinates of desired element>
```

`ASIZE`: takes the quoted array name and outputs the size of the array.

### Naming

`MAKE`: names a value. Takes the quoted name and a value, and assigns the value
to the name.

```
MAKE "FOO 1
```

The name is prefixed with the symbol `:` (pronounced "dots") to retrieve its 
value.

```
MAKE "FOO 1
?PRINT :FOO
1
```

This can also be done with the LOGO primitive `THING`:

```
MAKE "FOO 1
?PRINT THING FOO
```

The backarrow is an infixed form of `MAKE`:

```
?"FOO <- 1
```

is equivalent to 

```
?MAKE "FOO 1
```

The infix form of `MAKE` can be used to give multiple names to the same value:

```
?"A <- "B <- 1
?PRINT :A
1
?PRINT :B
1
```

#### Local and global names

A procedures parameter names can shadow names in the global namespace.

`LOCAL` can be used to declare a local variable.

```
TO COUNTSQUARES :X
5 LOCAL "XSQ
10 IF :X=0 STOP
20 MAKE "XSQ :X * :X
30 COUNTSQUARES :X-1
40 PRINT :XSQ
END
```

### Control

`GO`: this command can only be used in a procedure. It takes a number referring
to a line in that procedure and transfers control to that line.

`STOP`: terminates control of a procedure and transfers control to the calling
procedure.

`TOPLEVEL`: returns control in a procedure immediately to the top level.

`OUTPUT`: returns control to the calling procedure and outputs its argument. 
This is how users define procedures that are operations.

### Booleans, conditions, and related commands

`TRUE` and `FALSE` are just Logo words.

Conditionals are operations that output `TRUE` or `FALSE`.
You can make your own if you want.

LOGO has three classes of readymade conditionals:

- Numerical: see the section on numbers
- Logical: compute `TRUE` or `FALSE` from inputs that evaluate to `TRUE` or 
  `FALSE`.
    - `BOTH`: `TRUE` if both inputs are `TRUE`.
    - `EITHER`: `TRUE` if either input is `TRUE`.
    - `NOT`: one input, `TRUE` if the input is `FALSE`, else `FALSE`.
- Predicative
    - `WORDP`: `TRUE` if the input is a word.
    - `LISTP`: `TRUE` if the input is a list.
    - `EMPTYP`: `TRUE` if the input is empty.
    - `NUMBERP`: `TRUE` if the input is a number.

`IF`: takes one input. if the input evaluates to `TRUE`, the rest of the line is
evaluated. Otherwise the line is skipped. Adding `ELSE` to the line means that
the rest of the line is skipped except everything following `ELSE`.

```
TO CHECK :W
10 IF WORDP :W PRINT "WORD ELSE PRINT [NOT WORD]
END
?CHECK "FOO
WORD
?CHECK [FOO BAR BAZ]
NOT WORD
```

`TEST`: evaluates its input to `TRUE` or `FALSE` and puts the result in a "test
box".

`IFTRUE` and `IFFALSE` look into the test box and conditionally execute the 
line based on the test box's current state.

Test box properties:

- box is `FALSE` by default (if no test has been made)
- each subsequent invocation of `TEST` overwrites the last state
- each procedure has its own test box that is local to the procedure
- tests made in a subprocedure don't affect the test box of the calling 
  procedure

### Turtles

`TURTLE`: takes one input, the number of the turtle you want to use.

`NOTURTLE`: releases the turtle from your control so someone else can use it.

`FORWARD` (`FD`): takes one input, a number of units to move forward.

`BACK` (`BK`): like `FORWARD`, except moves backward.

`RIGHT` (`RT`): rotate n degrees clockwise.

`LEFT` (`LT`): rotate n degrees counterclockwise.

`PENUP` (`PU`) and `PENDOWN` (`PD`): raise and lower the turtle's pen. When the
pen is down, the turtle draws when it moves.

`LAMPON` and `LAMPOFF` toggles the turtle's headlight on and off.

`TOOT`: blow the turtle's whistle n number of times.

`FTOUCH`: outputs true if the front of the turtle is touching something.

`BTOUCH`: back touch

`RTOUCH`: right touch

`LTOUCH`: left touch

Here's a procedure that bounces the turtle between two walls:

```
TO BOUNCE
10 TEST FTOUCH
20 IFTRUE LEFT 180 ! TURN AROUND
30 FORWARD 10
40 BOUNCE
END
```

**Seeing-eye turtle**

`LIGHT`: outputs the light level, a number from 0 to 63

`LAMPON` and `LAMPOFF`: enable/disable a mode where the light sensor ("eye") can
rotate.

`FORWARD`, `BACK`, `LEFT`, `RIGHT`: orient the light sensor.

**Light box**

An area surrounded by lightbulbs.
An environment for the seeing-eye turtle.
The lightbulb border can be used by the seeing-eye turtle for orientation.

```
.TYO "LIGHT <number>
``` 

Converts `<number>` to binary and uses each bit to determine the on/off state of
a light in the wall.

**Display turtles**

These are virtual rather than physical turtles.

`CLEARSCREEN` (`CS`): erases everything on the display screen and places the 
turtle at the center of the screen facing up.
Call `CS` before issuing any other commands to the turtle.

`WIPECLEAN` (`WC`): erases everything on the display screen but leaves the 
turtle where it is.

`NODISPLAY` (`ND`): turns off the display.

`STARTDISPLAY`: takes one input, 0 or 1. 
0 starts a half-size display. 
1 starts a fullsize display.

`WRAP` and `NOWRAP`: toggle a mode so that if the turtle goes past the edge of 
the screen, its path wraps around to the opposite side.

`HIDETURTLE` (`HT`) and `SHOWTURTLE` (`ST`): the turtle will continue to draw 
lines if the pen is down but will not be visible on the screen.

### Snaps

A snap is a reference to the "stuff currently displayed on the screen."
It's like taking a picture of the screen.

```
MAKE "PIC SNAP
```

`DISPLAY`: show a snap

```
DISPLAY :PIC
```

Each snap has a starting location.
The starting location is normally the center of the screen.
The snap is displayed at the current turtle position and the turtle is moved to 
the end of the SNAP.


`WIPE`: removes all instances of a snap from the screen.

```
WIPE :PIC
```

`NEWSNAP`: causes the image currently on the screen not to be part of subsequent
snaps.
Sets the starting position of subsequent snaps to the current position of the
turtle rather than the center of the screen.

### Locating the turtle

`HERE`: outputs a list of the x-coordinate, y-coordinate, and heading of the
turtle.

`HERE` can be used to "name" or "bookmark" a place on the screen:

```
MAKE :FOO HERE
```

`SETTURTLE` (`SETT`) is used to move the turtle to a location.
It takes a list of an x-coordinate, a y-coordinate, and a heading.
The turtle will draw a line if the pen is down.

The `HOME` command is equivalent to `SETT [0 0 1]`.

`XCOR` outputs the current x-coordinate.

`YCOR` outputs the current y-coordinate.

`SETX` takes a number and moves the turtle to that x-coordinate.

`SETY` takes a number and moves the turtle to that y-coordinate.

`SETXY` takes two numerical inputs and moves the turtle to that location.

`HEADING` outputs the number (in degrees) of the turtle.

`SETHEADING` sets the heading of the turtle.

### Multiple device control

It is possible to control more than one device from a LOGO program.

`ALSO <device>` gives you access to an additional device. 
After calling `ALSO`, you must call `CNTRL <device>` to specify which of your 
multiple devices you are currently issuing commands to.

```
ALSO TURTLE <number>
CNTRL TURTLE <number>
```

### Music box

LOGO has a way to play music.
There are 4 "voices" that can sound simultaneously.
Each voice has a range of 5 chromatic octaves.

`NOTE <pitch> <duration>`: generates one note of music.
Both arguments are numeric.

`NOTE` can also take multiple pairs of input using the parentheses notation:

```
(NOTE :P :D :P :D ...)
```

**Pitch**

Pitches are numbered chromatically from -24 to 36 with 0 being middle C.

Three special pitches:

- `-28` is silence
- `-27` percussion sound "boom"
- `-26` percussion sound "ssh"
- `-25` not a valid pitch

**Duration**

Must be between 0 and 127 units.
Each unit is normally about 1/8 of a second.

For a duration greater than 1, each unit of duration is followed by 1 unit of 
rest to prevent a slurring sound.

Percussion sounds are followed by 1 unit of rest, making it easier to make a 
beat.

`NOTE` does not cause the notes to be played. 
The notes are buffered in a "music box".
`PM` is used to play the notes that have been buffered in the music box.
`MCLEAR` will empty the music buffer.

### Voice

The LOGO environment has 4 voices that can be played in parallel.

`VOICE <voice number> <note>` is used to assign a note to a voice.
You can send notes to multiple voices this way:

```
VOICE 1 <note>
VOICE 2 <note>
VOICE 3 <note>
```

`VLEN`: outputs the total length of the music for the current voice.

`MLEN`: outputs the length for the largest voice.

Here is a procedure that generates oompahs for as long as the largest voice 
lasts.

```
TO OOMPAH
10 VOICE 2
20 (NOTE O 4 (-5) 4)
30 IF VLEN = MLEN STOP ELSE GO 20
END
```

### Lists

Lists can contains lists.

```
[FOO [BAR] [[BAZ]]]
```

`PRINT` does not print the outer brackets of a list.

```
?PRINT [FOO [BAR] [[BAZ]]]
FOO [BAR] [[BAZ]]
```

`COUNT`: outputs the length of a list.

`LIST` takes two inputs and outputs a two-element list.

`FPUT`: takes two inputs, the second must be a list. Creates a new list by 
adding the first argument to the second argument.

```
?FPUT "FOO [BAR BAZ]
[FOO BAR BAZ]

?FPUT [FOO] [BAR BAZ]
[[FOO] BAR BAZ]
```

`LPUT`: like `FPUT` except adds first argument to the end of the second 
argument.

```
?LPUT "BAZ [FOO BAR]
[FOO BAR BAZ]

?LPUT [BAZ] [FOO BAR]
[FOO BAR [BAZ]]
```

Lists can contain snaps.

### Error handling

`ERSET`: takes one argument, a procedure name (note quoted).
The given procedure will be executed every time an error occurs.
If the given procedure does not output, LOGO then prints the original error 
message.
If the given procedure does output, LOGO prints the output.

`ERCLR`: deactivates `ERSET`.

`ERRET`: takes a line number and returns execution to that line of the procedure
when an error occurs.

`ERLIN`: outputs the line in which the error occurred.

This reexecutes the line containing the error:

```
ERRET ERLIN
```

`ERNAM`: outputs the name of an error.

`ERPRO`: outputs the name of the procedure in which the last error occurred.

`ERBRK`: outputs 1 if the error was caused by ctl-G, -1 if hte error was caused
by ctl-Z, and 0 otherwise.

`ERNUM`: each type of error has a corresponding number. this outputs the number
of the error that just occurred.

`ERTOK`: outputs the token number at which the previous error occurred in the
line.

### Evaluating text

You can evaluate a LOGO program from within a LOGO program.

`RUN`: takes a list and evaluates the list as if it were typed into the console.

### Procedures defining procedures

It's valid for a procedure to define another procedure:

```
TO DRIB
10 TO DRAB
20 10 PRINT "WHOOPIE
30 END
END
?DRIB
?DRAB
WHOOPIE
```

```
TO DEFINE :PROC :N
10 RUN SENTENCE "TO :PROC
20 RUN SENTENCE [10 PRINT] :N
30 END
END
?DEFINE "WHAMO 7
?WHAMO 
7
```

I don't understand here why line `30` doesn't need an `RUN` command.

### Miscellaneous commands

`BELL`: rings the terminal bell

`CLOCK`: outputs a number incremented every 1/60 of a second

`DATE`: outputs a 3-element list containing month, day, and year.

`TIME`: outputs a 3-element list containing hour, minute, second.

`WAIT`: takes a number and causes the execution to suspend for that many 1/30
second intervals.

`HELLO`: clears the workspace and resets the LOGO environment.

`GOODBYE`: same as `HELLO`.

`LEVEL`: outputs a number representing how many levels deep in procedures 
the execution is.

`PEEK`: prints out system status information.

`MAIL`: enables users to send messages to other users.
